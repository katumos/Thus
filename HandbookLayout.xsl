<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" 
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
                xmlns:fo="http://www.w3.org/1999/XSL/Format"
                xmlns:my="http://www.sbsa.gov.uk/XMLSYS/Handbook#1"
                xmlns:std="http://www.concept.co.uk/std"
                >

  <xsl:output method="xml" version="1.0" encoding="UTF-8" standalone="yes" indent="yes"/>

  <xsl:include href="STD.xsl" />
  <xsl:include href="ChapterTypes.xsl" />
  <xsl:include href="ChapterFonts.xsl" />
  <xsl:include href="HandbookTOC.xsl" />

  <!--

    Page Dimensions & Layout Variables

  -->
  
  <!-- Page Size A4 -->
  <xsl:variable name="page-width" select="'21cm'" />
  <xsl:variable name="page-height" select="'29.7cm'" />
  <xsl:variable name="chapter-marginref-area-width" select="'4.2cm'" />
  <xsl:variable name="chapter-text-area-width" select="'13.3cm'" />
  <xsl:variable name="chapter-text-area-sub-width" select="'6.4cm'" />
  <xsl:variable name="chapter-text-area-sub-width-wide" select="'8.5cm'" />
  <xsl:variable name="chapter-text-side-by-side-spacer" select="'0.5cm'" />
  <xsl:variable name="chapter-page-sbs-content-width" select="'10.6cm'" />
  <xsl:variable name="chapter-page-content-width" select="'17.5cm'" />
  <xsl:variable name="chapter-page-content-height" select="'24.2cm'" />
  
  <!-- Page Output -->
  
  <xsl:template match="/my:Document/my:Handbook" >
  
  <xsl:comment>
  Generated by HandbookLayout.xsl [THUS XML System - Release 1.3.0] (c) 2006 SBSA
  </xsl:comment> 
    
    <fo:root xmlns:fo="http://www.w3.org/1999/XSL/Format" >
    
      <fo:layout-master-set>
      
        <fo:simple-page-master  master-name="blank.page"
                                page-width="{$page-width}" 
                                page-height="{$page-height}" >
          <fo:region-body />
        </fo:simple-page-master>
        
        <fo:simple-page-master  master-name="toc.page" 
                                page-width="{$page-width}" 
                                page-height="{$page-height}" 
                                margin-left="2.3cm"
                                margin-right="2.3cm" >
          <fo:region-before extent="3cm" precedence="true" />
          <fo:region-start extent="3.1cm" />
          <fo:region-body margin-top="3cm" margin-bottom="2.5cm" margin-left="3.1cm" />
          <fo:region-after extent="2.5cm" region-name="footer.toc" precedence="true" />
        </fo:simple-page-master>
        
      </fo:layout-master-set>

      <xsl:call-template name="HandbookTOC" />

      <fo:page-sequence master-reference="blank.page" >
        <fo:flow flow-name="xsl-region-body" >
          <fo:block />
        </fo:flow>
      </fo:page-sequence>

    </fo:root>
    
  </xsl:template>

  <xsl:template name="PageHeader" >
    <fo:static-content flow-name="xsl-region-before">
      <fo:table border-collapse="collapse" table-layout="fixed" inline-progression-dimension.minimum="100%" >
        <fo:table-column column-number="1" width="100%" />
        <fo:table-body>
          <fo:table-row height="1.6cm" display-align="after" >
            <fo:table-cell>
              <fo:block text-align="start" xsl:use-attribute-sets="PageHeaderFont" >
                <xsl:apply-templates select="/my:Document/my:Handbook" mode="TYPE" />
                <xsl:text> | contents | </xsl:text>
                <xsl:apply-templates select="/my:Document/my:Handbook" mode="YEAR" />
              </fo:block>
            </fo:table-cell>
          </fo:table-row>
        </fo:table-body>
      </fo:table>
    </fo:static-content>
  </xsl:template>
  
  <xsl:template match="my:Handbook" mode="TYPE" >
    <xsl:choose>
      <xsl:when test="@handbookType = $handbook-type-domestic" >domestic</xsl:when>
      <xsl:when test="@handbookType = $handbook-type-non-domestic" >non-domestic</xsl:when>
      <xsl:otherwise>
        <fo:inline color="red">unknown</fo:inline>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
  
  <xsl:template match="my:Handbook" mode="YEAR" >
    <xsl:choose>
      <xsl:when test="string-length( my:HandbookYear ) = 0">
        <fo:inline color="red">unknown</fo:inline>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="my:HandbookYear" />
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
  
</xsl:stylesheet>
